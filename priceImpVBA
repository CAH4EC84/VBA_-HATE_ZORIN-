' SCRIPT 10-07-19 Alexander Golikov
'7z settings
Dim zPath
	zPath = "Z:\7-Zip\7z.exe "
'Outlook settings
Dim mailAcc
	mailAcc = "medlineuser@price.medline.spb.ru"
Dim inbox 
	inbox = "Входящие"
Dim arc 
	arc = "Archive"

'Shell
Set oShell = CreateObject("WScript.Shell")

'Oulook
Set OlApp = WScript.CreateObject("Outlook.Application")
Set NameSpace = OlApp.getNamespace("MAPI")
Set accountFolder = NameSpace.folders(mailAcc)
Set inboxFolder = accountFolder.folders(inbox)
Set arcFolder = accountFolder.folders(arc)

'DB Settings
Dim WorkDir
	WorkDir = "z:\"
Dim sDBPath
	sDBPath = WorkDir & "prices_2000.accdb"
Dim sConnString
	sConnString = "Provider=Microsoft.ACE.OLEDB.12.0;Data Source=" & sDBPath
Dim sQuery 
	sQuery ="SELECT DISTINCT pricemail,ident, arx,fileext,extrcmd   FROM _Info;"
'Open Connection get table _Info
Set dbConn = CreateObject("ADODB.Connection")
dbConn.ConnectionString = sConnString
dbConn.Open

'Execute SQL Query & Get Records matching the query to recordset
Set dbRecSet = CreateObject("ADODB.Recordset")
dbRecSet.Open sQuery, dbConn

While inboxFolder.items.count > 0 'Iterate from incoming mails
	For Each item In inboxFolder.items
		If item.attachments.count > 0  Then 'check mail only with attach
			For Each recipient In item.recipients
				addrFound = False
				WScript.Echo (chr(13) &"check mail->" & recipient.Address)
				dbRecSet.MoveFirst
				While (Not dbRecSet.EOF and not addrFound)'scan table for email					
					If UCase(recipient.Address) = UCase(dbRecSet.Fields(0).Value) Then 'found client
						addrFound = True
						WScript.Echo ("valid adress found")
						For Each attach In item.attachments						
						WScript.Echo ("attach check - " & attach  )
							If InStr(1,attach,dbRecSet.Fields(2).Value,vbTextCompare) Then 'attach valid format
								WScript.Echo ("valid attach found")
								If UCase(dbRecSet.Fields(2).Value) = UCase(dbRecSet.Fields(3).Value) Then 'check unzip or just save
									savePath = WorkDir & dbRecSet.Fields(1).Value & "\"
									saveArc = savePath  & dbRecSet.Fields(1).Value & "." & dbRecSet.Fields(3).Value
									WScript.Echo ("just saved ->" & saveArc)							
									attach.SaveAsFile(saveArc)
								Else 'unzip
									savePath = WorkDir & dbRecSet.Fields(1).Value & "\"
									saveArc = savePath  & "Archive."&dbRecSet.Fields(2).Value
									attach.SaveAsFile(saveArc)							
									'Extract file from arc ussing 7z
									extCmd ="cmd /c " & zPath &" e -so " & saveArc &  " > " & savePath & dbRecSet.Fields(1).Value & "." & dbRecSet.Fields(3).Value
									WScript.Echo ("extCmd->" & extCmd)
									oShell.Run  extCmd,0,True
								End If
							End If
						Next
					End If
					dbRecSet.MoveNext
				Wend
			Next
			item.move(arcFolder)
		End If
	Next
Wend


'Cleaning
Set OlApp = Nothing
dbRecSet.Close
Set dbRecSet = Nothing
dbConn.Close
Set dbConn = Nothing
Set oShell = Nothing



